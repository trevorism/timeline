buildscript {
    ext {
        creds = {
            username = findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
            password = findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url uri("https://maven.pkg.github.com/trevorism/gradle-acceptance-plugin")
            credentials creds
        }
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.4.5'
        classpath 'com.trevorism:gradle-acceptance-plugin:2.3.1'
    }
}

plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.7.8"
    id("com.github.node-gradle.node") version "3.5.1"
}

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: "com.trevorism.gradle.acceptance"

group = "com.trevorism"

repositories {
    mavenCentral()
    maven {
        url uri("https://maven.pkg.github.com/trevorism/secure-http-utils")
        credentials creds
    }
}

dependencies {
    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.security:micronaut-security")
    implementation("io.micronaut:micronaut-validation")

    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation 'io.projectreactor:reactor-core:3.5.5'

    implementation("io.swagger.core.v3:swagger-annotations")
    compileOnly("io.micronaut.openapi:micronaut-openapi"){
        exclude module:"slf4j-nop"
    }

    runtimeOnly("ch.qos.logback:logback-classic")
    implementation 'org.slf4j:slf4j-simple:2.0.6'

    acceptanceImplementation 'io.cucumber:cucumber-groovy:6.10.4'
}

application {
    mainClass.set("com.trevorism.Application")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.trevorism.*")
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
    groovyOptions.forkOptions.jvmArgs.add('-Dmicronaut.openapi.views.spec=rapidoc.enabled=false,swagger-ui.enabled=true,swagger-ui.theme=flattop')
}

appengine {
    stage.artifact = "${buildDir}/libs/${project.name}-all.jar"
    deploy {
        projectId = "trevorism-draw"
        version = "0-1-0"
    }
}

node{
    nodeProjectDir = file("${project.projectDir}/src/app")
}

task installNodeDependencies(type: NpmTask) {
    group = "npm"
    args = ['install']
}

task buildApp(type: NpmTask) {
    dependsOn('installNodeDependencies')
    outputs.dir("src/app/dist")
    group = "npm"
    args = ['run', 'build']
}

task testApp(type: NpmTask) {
    group = "npm"
    args = ['run', 'accept']
}

task runApp(type: NpmTask) {
    group = "npm"
    args = ['run','dev']
}

task copyFrontEnd(type: Copy){
    dependsOn("buildApp")
    mustRunAfter(compileGroovy)
    from "src/app/dist"
    into "${buildDir}/resources/main/public"
}


jar.dependsOn("copyFrontEnd")
run.dependsOn("copyFrontEnd")
buildLayers.dependsOn("copyFrontEnd")
shadowJar.dependsOn("copyFrontEnd")
compileTestGroovy.mustRunAfter('copyFrontEnd')
compileAcceptanceGroovy.mustRunAfter('copyFrontEnd')